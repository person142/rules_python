# Copyright 2017 The Bazel Authors. All rights reserved.
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#    http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.
"""Import pip requirements into Bazel."""

load("//python/pip_install:pip_repository.bzl", "pip_repository")
load("//python/pip_install:repositories.bzl", "pip_install_dependencies")

def pip_install(requirements, name = "pip", repo_prefix = None, **kwargs):
    """Imports a `requirements.txt` file and generates a new `requirements.bzl` file.

    This is used via the `WORKSPACE` pattern:

    ```python
    pip_install(
        requirements = ":requirements.txt",
    )
    ```

    You can then reference imported dependencies from your `BUILD` file with:

    ```python
    load("@pip//:requirements.bzl", "requirement")
    py_library(
        name = "bar",
        ...
        deps = [
           "//my/other:dep",
           requirement("requests"),
           requirement("numpy"),
        ],
    )
    ```

    Args:
      requirements: A 'requirements.txt' pip requirements file.
      name: A unique name for the created external repository (default 'pip').
      repo_prefix: prefix for the packages generated in the external repository
      **kwargs: Keyword arguments passed directly to the `pip_repository` repository rule.
    """

    # Just in case our dependencies weren't already fetched
    pip_install_dependencies()

    if repo_prefix == None:
        repo_prefix = "pypi__"

    pip_repository(
        name = name,
        requirements = requirements,
        repo_prefix = repo_prefix,
        **kwargs
    )

def pip_parse(requirements_lock, name = "pip_parsed_deps", repo_prefix = None, **kwargs):
    """Generate external repositories for each entry in a requirements lockfile.

    Args:
      requirements_lock: a requirements lockfile, as generated by e.g. pip-compile
      name: name for the initial repository that generates
        repositories for each entry of the lockfile
      repo_prefix: prefix for the generated external repositories
      **kwargs: keyword arguments passed directly to the `pip_repository` repository rule.

    """

    # Just in case our dependencies weren't already fetched
    pip_install_dependencies()

    if repo_prefix == None:
        repo_prefix = "{name}_pypi__".format(name = name)

    pip_repository(
        name = name,
        requirements_lock = requirements_lock,
        incremental = True,
        repo_prefix = repo_prefix,
        **kwargs
    )

def pip_repositories():
    # buildifier: disable=print
    print("DEPRECATED: the pip_repositories rule has been replaced with pip_install, please see rules_python 0.1 release notes")

def pip_import(**kwargs):
    fail("=" * 79 + """\n
    pip_import has been replaced with pip_install, please see the rules_python 0.1 release notes.
    To continue using it, you can load from "@rules_python//python/legacy_pip_import:pip.bzl"
    """)
